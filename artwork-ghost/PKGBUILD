# Maintainer: Ramon Buld√≥ <ramon@manjaro.org>

pkgbase=artwork-ghost
pkgname=('plasma5-themes-ghost' 'sddm-ghost-theme' 'gtk-themes-ghost')
pkgver=0.0.1
pkgrel=11
_gitcommit=760a922d22e503f6dfd4c39eeae0a076687b31ee
url=https://github.com/Strit/artwork-ghost/
arch=('any')
license=('LGPL')
makedepends=('extra-cmake-modules' 'plasma-framework')

source=("ghost-$pkgver-$pkgrel.tar.gz::$url/archive/$_gitcommit.tar.gz")
md5sums=('SKIP')

prepare() {
  mv $srcdir/artwork-ghost-$_gitcommit $srcdir/ghost
  mkdir -p build
}

build() {
  cd build
  cmake ../ghost \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}


package_plasma5-themes-ghost() {
  pkgdesc='Ghost theme for KDE Plasma 5'
  depends=('breeze' 'gtk-themes-ghost')
  optdepends=('breath-icon-theme: light icon theme'
              'breath-dark-icon-theme: dark icon theme')
  replaces=('ghost-themes')
  cd build
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/usr/share/sddm"
  rm -rf "${pkgdir}/usr/share/icons"
  rm -rf "${pkgdir}/usr/share/wallpapers"
}

package_sddm-ghost-theme() {
  pkgdesc="Ghost theme for SDDM"
  depends=('plasma-framework' 'plasma-workspace' 'plasma5-themes-ghost' 'sddm')
  cd build
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/usr/share/color-schemes"
  rm -rf "${pkgdir}/usr/share/icons"
  rm -rf "${pkgdir}/usr/share/kservices5"
  rm -rf "${pkgdir}/usr/share/metainfo"
  rm -rf "${pkgdir}/usr/share/plasma"
  rm -rf "${pkgdir}/usr/share/wallpapers"
  rm -rf "${pkgdir}/usr/share/aurorae/"
} 

package_gtk-themes-ghost() {
  pkgdesc="Ghost theme for gtk2 and gtk3"
  depends=('gtk2' 'gtk3')

  install -dm755 ${pkgdir}/usr/share/themes/
  cp -r ${srcdir}/ghost/Ghost "${pkgdir}/usr/share/themes/"
}
